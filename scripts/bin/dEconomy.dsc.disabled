#
# dEconomy
# author: Moepius
# version: 1.0.0
#
# READ ME
# this script provides basic economy functions and requires Vault to run
#
#
#




# The script name will be shown to the economy provider as the name of the economy system.
deconomy_Handler:
    type: economy
    debug: false
    # The Bukkit service priority. Priorities are Lowest, Low, Normal, High, Highest.
    priority: highest
    # The name of the currency in the singular (such as "dollar" or "euro").
    name single: $ilberling
    # The name of the currency in the plural (such as "dollars" or "euros").
    name plural: $ilberlinge
    # How many digits after the decimal to include. For example, '2' means '1.05' is a valid amount, but '1.005' will be rounded.
    digits: 0
    # Format the standard output for the money in human-readable format. Use "<amount>" for the actual amount to display.
    # Fully supports tags.
    format: <amount> $ilberlinge
    # A tag that returns the balance of a linked player. Use a 'proc[]' tag if you need more complex logic.
    # Must return a decimal number.
    balance: <player.flag[money]>
    # A tag that returns a boolean indicating whether the linked player has the amount specified by auto-tag "<amount>".
    # Use a 'proc[]' tag if you need more complex logic.
    # Must return 'true' or 'false'.
    has: <player.flag[money].is[or_more].than[<amount>]>
    # A script that removes the amount of money needed from a player.
    # Note that it's generally up to the systems calling this script to verify that the amount can be safely withdrawn, not this script itself.
    # However you may wish to verify that the player has the amount required within this script.
    # The script may determine a failure message if the withdraw was refused. Determine nothing for no error.
    # Use def 'amount' for the amount to withdraw.
    withdraw:
    - flag <player> money:-:<[amount]>
    # A script that adds the amount of money needed to a player.
    # The script may determine a failure message if the deposit was refused. Determine nothing for no error.
    # Use def 'amount' for the amount to deposit.
    deposit:
    - flag <player> money:+:<[amount]>


Money_Command:
    type: command
    name: money
    debug: true
    usage: /money
    description: Ein Service der Bank Eures Vertrauens *Zwinker.
    tab completions:
        1: <tern[<player.has_permission[deconomy.moneyadmin]>].pass[balance|pay|give|take|top|set].fail[balance|pay]>
        2: <server.online_players.parse[name]>
    script:
    - if <context.args.size> == 0:
        - narrate "Ihr besitzt <gold><player.money> $ilberlinge."
        - stop
    - if <context.args.size> > 0 && <context.args.get[1]> != top:
        - define player <server.match_offline_player[<context.args.get[2]>]||null>
        - if <[player]> == null:
            - narrate "<red>Ungültiger Spielername! <green>/money balance Spielername"
            - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
            - stop
        - if <context.args.get[1]> == balance:
            - if <context.args.size> == 1:
                - narrate "Ihr besitzt  <gold><player.money> $ilberlinge."
                - stop
            - narrate "<[player].name>'s Kontostand:  <gold><[player].money> $ilberlinge."
            - stop
        - define amount <context.args.get[3]||0>
        - if !<[amount].is_integer> || <[amount]> <= 0:
            - narrate "<red>Ungültige Summe - keine ganze Zahl oder kleiner als 1!"
            - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
            - stop
        - if <context.args.get[1]> == pay:
            - define sender <player>
            - define recipient <[player]>
            - if <context.args.size> == 1:
                - narrate "<red>Ihr müsst einen Spieler und eine Summe angeben, z. B. <green>/money pay BillGates 5"
                - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
                - stop
            - if <player.money> >= <context.args.get[3]>:
                - take money quantity:<context.args.get[3]>
                - give money quantity:<context.args.get[3]> player:<[recipient]>
                - narrate "<green>Es wurden <context.args.get[3]> $ilberlinge an <[recipient].name> geschickt."
                - narrate "<green>Euch wurden <context.args.get[3]> $ilberlinge von <[sender].name> geschickt." player:<[recipient]>
                - stop
            - else:
                - narrate "<red>Zu wenig $ilberlinge in Eurem Geldbeutel! Ihr besitzt lediglich <gold><player.money> $ilberlinge<red>!"
                - stop
        - if <context.args.get[1]> == give:
            - if !<player.has_permission[deconomy.moneyadmin]>:
                - narrate "<red>Euch fehlt die Berechtigung für diesen Befehl :/."
                - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
                - stop
            - if <context.args.size> == 1:
                - narrate "<red>Ihr müsst einen Spieler und eine Summe angeben, z. B. <green>/money pay BillGates 5"
                - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
                - stop
            - define sender <player>
            - give money quantity:<context.args.get[3]> player:<[player]>
            - narrate "<green><context.args.get[3]> $ilberlinge an <[player].name> übertragen."
            - narrate "<green><context.args.get[3]> $ilberlinge von <[sender].name> erhalten." player:<[player]>
        - if <context.args.get[1]> == take:
            - if !<player.has_permission[deconomy.moneyadmin]>:
                - narrate "<red>Euch fehlt die Berechtigung für diesen Befehl :/."
                - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
                - stop
            - if <context.args.size> == 1:
                - narrate "<red>Ihr müsst einen Spieler und eine Summe angeben, z. B. <green>/money pay BillGates 5"
                - stop
            - if <[amount]> > <[player].money>:
                - narrate "<red>Summe, die abgezogen werden soll, ist größer als die Summe des Gesamtvermögens von  <gold><[player].money> $ilberlinge<red>!"
            - take money quantity:<context.args.get[3]> player:<[player]>
            - narrate "<green><context.args.get[3]> $ilberlinge von <[player].name> abgezogen."
        - if <context.args.get[1]> == set:
            - if !<player.has_permission[deconomy.moneyadmin]>:
                - narrate "<red>Euch fehlt die Berechtigung für diesen Befehl :/."
                - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
                - stop
            - if <context.args.size> != 3:
                - narrate "<red>Ihr müsst einen Spieler und eine Summe angeben, z. B. <green>/money pay BillGates 5"
                - stop
            - flag <[player]> money:<context.args.get[3]>
            - narrate "Setze <[player].name>'s Geldbörse auf <[amount]>"
    - if <context.args.get[1]> == top:
        - if !<player.has_permission[deconomy.moneyadmin]>:
            - narrate "<red>Euch fehlt die Berechtigung für diesen Befehl :/."
            - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
            - stop
        - if <context.args.size> == 2:
            - if !<context.args.get[2].is_integer> || <context.args.get[2]> <= 0 || <context.args.get[2]> > 91:
                - narrate "<red>Eure Liste muss zwischen 1 und 91 starten!"
                - playsound <player> sound:ENTITY_VILLAGER_NO pitch:1
                - stop
            - narrate <empty>
            - narrate <empty>
            - narrate " <gold><bold><underline>Craftasys größte Geldbörsen (angefangen bei <context.args.get[2]>)"
            - narrate <empty>
            - narrate <empty>
            - foreach <server.flag[money_top_100].highest[money].count[100].get[<context.args.get[2]>].to[<context.args.get[2].add[9]>]>:
                - narrate "<[loop_index].add[<context.args.get[2]>].sub[1]>. <[value].name>:  <gold><[value].money> $ilberlinge"

        - else:
            - narrate <empty>
            - narrate <empty>
            - narrate " <gold><bold><underline>Craftasys größte Geldbörsen"
            - narrate <empty>
            - narrate <empty>
            - foreach <server.flag[money_top_100].highest[money].count[10]>:
                - narrate "<[loop_index]>. <[value].name>:  <gold><[value].money> $ilberlinge"

Money_Missing_Fixer:
    type: world
    debug: false
    events:
        on player joins:
        - if !<player.has_flag[money]>:
            - flag player money:100
        - if !<player.money.is_integer>:
            - flag player money:<player.money.round_down>

Money_Top_Handler:
    type: world
    debug: false
    events:
        on delta time hourly:
        - flag server money_top_100:<server.players.highest[money].count[100].sort_by_number[money]>